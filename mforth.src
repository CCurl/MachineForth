\ boot-strap stuff ...
\ #01 => lit, #02 => clit
\ #13 => jmp, #14 => jmpz, #15 => jmpnz, #17 ret
(h) @ #05 - dw the-memory #01 c, , #17 c,

\ constant
#04 dw cell #02 c, c, #17 c, inline

\ define some variables
dw s $01 c, (h) @ 0 , #17 c, dw (s) $01 c, , #17 c,
dw d $01 c, (h) @ 0 , #17 c, dw (d) $01 c, , #17 c,

\ ok, now we can start writing the language in itself
: >s (s) ! ; : >d (d) ! ;
: here (h) @ ; inline

\ program control
: if   #14 c, here 0 , ; immediate
: else #13 c, here swap 0 , here swap ! ; immediate
: then here swap ! ; immediate

: begin here ; immediate
: again #13 c, , ; immediate
: until #14 c, , ; immediate
: while #15 c, , ; immediate

\ Machine Forth words
: @+ s @ s 4 + >s ; : c@+ s c@ s 1+ >s ;
: !+ d ! d 4 + >d ; : c!+ d c! d 1+ >d ;

\ core words
: space $20 emit ; : bl $20 ; : cr #13 emit #10 emit ;
: . space (.) ;
: hex $10 base ! ; : decimal #10 base ! ;
: hex.     base @ hex     swap . base ! ;
: decimal. base @ decimal swap . base ! ;
: strlen ( a -- n ) >s 0 >r begin c@+ if drop r> 1+ >r then while drop r> ;
: count ( a1 -- a2 n ) 1+ dup 1- c@ ;
: type ( addr n -- ) swap >s begin c@+ emit 1- while drop ;

\ navigating the dictionary
: >xt @ ; : >flags 4 + c@ ; : >len 5 + ; : >name 6 + ; : >next #36 - ;
: .word ( addr -- ) dup >len space count type ;
: .wordl ( addr -- ) cr dup >xt hex. dup >flags . dup >len space count type ;
: words-each last 0 begin drop ( do something ) >next dup >xt while drop drop ;
: words      last 0 begin drop    dup .word     >next dup >xt while drop drop ;
: wordsl     last 0 begin drop    dup .wordl    >next dup >xt while drop drop ;

\ for the REPL
: .. dup . ;
: .s space '(' emit >r >r >r >r .. r> .. r> .. r> .. r> .. space ')' emit ;
: pow ( n1 n2 -- n3 ) 1 >r begin over r> * >r 1- while drop drop r> ;
: .2 dup base @       < if '0' emit then drop (.) ;
: .3 dup base @ 2 pow < if '0' emit then drop .2 ;
: .4 dup base @ 3 pow < if '0' emit then drop .3 ;
: mil ( n1 -- n2 ) #1000 #1000 * * ;
: timer gettick ;
: elapsed swap - 1000 /mod space (.) '.' emit .3 ;
: bench ( n -- ) timer swap begin 1- while drop timer elapsed ;
: benches ( n count -- ) begin over bench 1- while drop drop ;
: main 500 mil 10 benches ;